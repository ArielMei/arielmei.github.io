{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Chapter 1\n",
    "## Querying a Table\n",
    "```SAS\n",
    "proc sql;\n",
    "    select empid,jobcode,salary, salary*.06 as bonus \n",
    "    from sasuser.payrollmaster \n",
    "    where salary<32000\n",
    "    order by jobcode;\n",
    "quit;\n",
    "```\n",
    "## Summarizing Groups of Data\n",
    "```SAS\n",
    "proc sql;\n",
    "    select membertype, sum(milestraveled) as TotalMiles \n",
    "    from sasuser.frequentflyers\n",
    "    group by membertype;\n",
    "quit;\n",
    "```\n",
    "## Creating a Table from the Results of a Query on Two Tables\n",
    "```SAS\n",
    "proc sql;\n",
    "    create table work.miles as\n",
    "    select salcomps.empid, lastname, newsals.salary, newsalary\n",
    "    from sasuser.salcomps,sasuser.newsals \n",
    "    where salcomps.empid=newsals.empid \n",
    "    order by 2;\n",
    "quit;\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Displaying All Columns in Output and an Expanded Column List in the SAS Log\n",
    "```SAS\n",
    "proc sql feedback; \n",
    "    select *\n",
    "    from sasuser.staffchanges; \n",
    "quit;\n",
    "``` \n",
    "## Eliminating Duplicate Rows from Output\n",
    "```SAS\n",
    "proc sql;\n",
    "    select distinct flightnumber, destination\n",
    "    from sasuser.internationalflights\n",
    "    order by 1; \n",
    "quit;\n",
    "```\n",
    "## Subsetting Rows By Using Calculated Values\n",
    "```SAS\n",
    "proc sql outobs=10; validate\n",
    "    select flightnumber, date label=\"Flight Date\", destination, \n",
    "            boarded + transferred + nonrevenue as Total\n",
    "    from sasuser.marchflights\n",
    "    where calculated total between 100 and 150; \n",
    "quit;\n",
    "```\n",
    "## Subsetting Data By Using a Noncorrelated Subquery\n",
    "```SAS\n",
    "proc sql noexec; \n",
    "    select jobcode, avg(salary) as AvgSalary format=dollar11.2 \n",
    "    from sasuser.payrollmaster \n",
    "    group by jobcode\n",
    "    having avg(salary) > (select avg(salary) from sasuser.payrollmaster);\n",
    "quit;\n",
    "```\n",
    "## Subsetting Data By Using a Correlated Subquery\n",
    "```SAS\n",
    "proc sql;\n",
    "title 'Frequent Flyers Who Are Not Employees'; \n",
    "    select count(*) as Count\n",
    "    from sasuser.frequentflyers \n",
    "    where not exists\n",
    "        (select *\n",
    "        from sasuser.staffmaster \n",
    "        where name = trim(lastname)||', '||firstname);\n",
    "quit;\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Chapter 2\n",
    "## Combining Tables By Using an Inner Join\n",
    "```SAS\n",
    "proc sql outobs=15;\n",
    "title 'New York Employees';\n",
    "    select substr(firstname,1,1) || '. ' || lastname as Name, jobcode,\n",
    "    int((today() - dateofbirth)/365.25) as Age\n",
    "    from sasuser.payrollmaster as p, sasuser.staffmaster as s\n",
    "    where p.empid = s.empid and state='NY' \n",
    "    order by 2, 3;\n",
    "quit;\n",
    "```\n",
    "## Combining Tables By Using a Left Outer Join\n",
    "```SAS\n",
    "proc sql outobs=20;\n",
    "title 'All March Flights';\n",
    "    select m.date, m.flightnumber label='Flight Number', \n",
    "    m.destination label='Left', f.destination label='Right',\n",
    "    delay label='Delay in Minutes' \n",
    "    from sasuser.marchflights as m left join sasuser.flightdelays as f \n",
    "    on m.date=f.date and m.flightnumber= f.flightnumber\n",
    "    order by delay; \n",
    "quit;\n",
    "```\n",
    "## Overlaying Common Columns in a Full Outer Join\n",
    "```SAS\n",
    "proc sql;\n",
    "    select coalesce(p.empid, s.empid) as ID, firstname, lastname, gender \n",
    "    from sasuser.payrollmaster as p full join sasuser.staffmaster as s\n",
    "    on p.empid = s.empid\n",
    "    order by id; \n",
    "quit;\n",
    "```\n",
    "## Joining Tables By Using a Subquery and an In-Line View\n",
    "```SAS\n",
    "proc sql; \n",
    "    select empid\n",
    "    from sasuser.supervisors as m,\n",
    "        (select substr(jobcode,1,2) as JobCategory, state\n",
    "        from sasuser.staffmaster as s, sasuser.payrollmaster as p \n",
    "        where s.empid = p.empid and s.empid in\n",
    "            (select empid\n",
    "            from sasuser.flightschedule \n",
    "            where date='04mar2000'd and destination='CPH')\n",
    "        ) as c \n",
    "    where m.jobcategory = c.jobcategory and m.state = c.state;\n",
    "quit;\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Chapter 3\n",
    "## Combining Tables By Using an Inner Join\n",
    "```SAS\n",
    "proc sql outobs=15;\n",
    "title 'New York Employees';\n",
    "    select substr(firstname,1,1) || '. ' || lastname as Name,\n",
    "    jobcode, int((today() - dateofbirth)/365.25) as Age\n",
    "    from sasuser.payrollmaster as p, sasuser.staffmaster as s\n",
    "    where p.empid = s.empid and state='NY' \n",
    "    order by 2, 3;\n",
    "quit;\n",
    "```\n",
    "## Combining Tables By Using a Left Outer Join\n",
    "```SAS\n",
    "proc sql outobs=20;\n",
    "title 'All March Flights';\n",
    "    select m.date, m.flightnumber label='Flight Number', m.destination label='Left', \n",
    "           f.destination label='Right', delay label='Delay in Minutes' \n",
    "    from sasuser.marchflights as m left join sasuser.flightdelays as f \n",
    "    on m.date=f.date and m.flightnumber= f.flightnumber\n",
    "    order by delay; \n",
    "quit;\n",
    "```\n",
    "## Overlaying Common Columns in a Full Outer Join\n",
    "```SAS\n",
    "proc sql;\n",
    "    select coalesce(p.empid, s.empid) as ID, firstname, lastname, gender \n",
    "    from sasuser.payrollmaster as p full join sasuser.staffmaster as s\n",
    "    on p.empid = s.empid\n",
    "    order by id; \n",
    "quit;\n",
    "```\n",
    "## Joining Tables By Using a Subquery and an In-Line View\n",
    "```SAS\n",
    "proc sql; \n",
    "    select empid\n",
    "    from sasuser.supervisors as m,\n",
    "        (select substr(jobcode,1,2) as JobCategory, state\n",
    "        from sasuser.staffmaster as s, sasuser.payrollmaster as p \n",
    "        where s.empid=p.empid and s.empid in\n",
    "            (select empid\n",
    "            from sasuser.flightschedule where date='04mar2000'd\n",
    "            and destination='CPH')\n",
    "        ) as c \n",
    "    where m.jobcategory=c.jobcategory and m.state=c.state;\n",
    "quit;\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Chapter 4\n",
    "## Combining Tables Vertically\n",
    "```SAS\n",
    "proc sql;\n",
    "    select firstname, lastname\n",
    "    from sasuser.staffchanges \n",
    "    intersect all\n",
    "    select firstname, lastname\n",
    "    from sasuser.staffmaster; \n",
    "quit;\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Chapter 5\n",
    "## Creating an Empty Table By Defining Columns\n",
    "```SAS\n",
    "proc sql;\n",
    "    create table work.discount\n",
    "    (Destination char(3), BeginDate num Format=date9., \n",
    "    EndDate num format=date9., Discount num);\n",
    "quit;\n",
    "```\n",
    "## Creating an Empty Table That Is like Another Table\n",
    "```SAS\n",
    "proc sql;\n",
    "    create table work.flightdelays2\n",
    "    (drop=delaycategory destinationtype) like sasuser.flightdelays;\n",
    "quit;\n",
    "```\n",
    "## Creating a Table from a Query Result\n",
    "```SAS\n",
    "proc sql;\n",
    "    create table work.ticketagents as\n",
    "    select lastname, firstname, jobcode, salary\n",
    "    from sasuser.payrollmaster, sasuser.staffmaster \n",
    "    where payrollmaster.empid = staffmaster.empid and jobcode contains 'TA';\n",
    "quit;\n",
    "```\n",
    "## Displaying the Structure of a Table\n",
    "```SAS\n",
    "proc sql;\n",
    "    describe table work.discount;\n",
    "quit;\n",
    "```\n",
    "## Inserting Rows into a Table By Specifying Column Names and Values\n",
    "```SAS\n",
    "proc sql;\n",
    "    insert into work.discount\n",
    "    set destination='LHR', begindate='01MAR2000'd, enddate='05MAR2000'd, discount=.33\n",
    "    set destination='CPH', begindate='03MAR2000'd, enddate='10MAR2000'd, discount=.15;\n",
    "quit;\n",
    "```\n",
    "## Inserting Rows into a Table By Specifying Lists of Values\n",
    "```SAS\n",
    "proc sql;\n",
    "    insert into work.discount \n",
    "    (destination, begindate,enddate, discount) \n",
    "    values ('LHR','01MAR2000'd,'05MAR2000'd,.33) \n",
    "    values ('CPH','03MAR2000'd,'10MAR2000'd,.15);\n",
    "quit;\n",
    "```\n",
    "## Inserting Rows into a Table from a Query Result\n",
    "```SAS\n",
    "proc sql;\n",
    "    insert into work.payrollchanges2\n",
    "    select empid, salary, dateofhire\n",
    "    from sasuser.payrollmaster\n",
    "    where empid in ('1919','1350','1401');\n",
    "quit;\n",
    "```\n",
    "## Creating a Table That Has Integrity Constraints\n",
    "```SAS\n",
    "proc sql;\n",
    "    create table work.employees\n",
    "    (Name char(10),\n",
    "    Gender char(1),\n",
    "    HDate date label='Hire Date' not null, \n",
    "    constraint prim_key primary key(name), \n",
    "    constraint gender check(gender in ('M' 'F'))\n",
    "    );\n",
    "quit;\n",
    "```\n",
    "## Displaying Integrity Constraints for a Table\n",
    "```SAS\n",
    "proc sql;\n",
    "    describe table constraints work.discount4;\n",
    "quit;\n",
    "```\n",
    "## Updating Rows in a Table Based on an Expression\n",
    "```SAS\n",
    "proc sql;\n",
    "    update work.payrollmaster_new\n",
    "    set salary=salary*1.05\n",
    "    where jobcode like '__1'; \n",
    "quit;\n",
    "```\n",
    "## Updating Rows in a Table By Using a CASE Expression\n",
    "```SAS\n",
    "proc sql;\n",
    "    update work.payrollmaster_new\n",
    "    set salary=salary* \n",
    "    case\n",
    "        when substr(jobcode,3,1)='1' then 1.05\n",
    "        when substr(jobcode,3,1)='2' then 1.10\n",
    "        when substr(jobcode,3,1)='3' then 1.15\n",
    "        else 1.08 \n",
    "    end;\n",
    "quit;\n",
    "```\n",
    "## Updating Rows in a Table By Using a CASE Expression 2\n",
    "```SAS\n",
    "proc sql outobs=10;\n",
    "    select lastname, firstname, jobcode,\n",
    "        case substr(jobcode,3,1) when '1' then 'junior' \n",
    "             when '2' then 'intermediate' \n",
    "             when '3' then 'senior' \n",
    "             else 'none'\n",
    "        end as JobLevel\n",
    "    from sasuser.payrollmaster, sasuser.staffmaster \n",
    "    where staffmaster.empid = payrollmaster.empid;\n",
    "quit;\n",
    "```\n",
    "## Deleting Rows in a Table\n",
    "```SAS\n",
    "proc sql;\n",
    "    delete from work.frequentflyers2\n",
    "    where pointsearned-pointsused<=0; \n",
    "quit;\n",
    "```\n",
    "## Adding, Modifying, and Dropping Columns in a Table\n",
    "```SAS\n",
    "proc sql;\n",
    "    alter table work.payrollmaster4\n",
    "    add Age num\n",
    "    modify dateofhire date format=mmddyy10. \n",
    "    drop dateofbirth, gender;\n",
    "quit;\n",
    "```\n",
    "## Dropping a Table\n",
    "```SAS\n",
    "proc sql;\n",
    "    drop table work.payrollmaster4;\n",
    "quit;\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Chapter 6\n",
    "## Creating a Simple, Unique Index, and a Composite Index\n",
    "```SAS\n",
    "proc sql;\n",
    "    create unique index EmpID on work.payrollmaster(empid); \n",
    "    create index daily on work.marchflights(flightnumber,date); \n",
    "quit;\n",
    "```\n",
    "## Displaying Index Specifications\n",
    "```SAS\n",
    "proc sql;\n",
    "    describe table marchflights;\n",
    "quit;\n",
    "```\n",
    "## Determining Whether SAS Is Using an Index\n",
    "```SAS\n",
    "options msglevel=i; \n",
    "proc sql;\n",
    "    select *\n",
    "    from marchflights\n",
    "    where flightnumber='182';\n",
    "quit;\n",
    "```\n",
    "## Directing SAS to Ignore All Indexes\n",
    "```SAS\n",
    "proc sql; \n",
    "    select *\n",
    "    from marchflights (idxwhere=no)\n",
    "    where flightnumber='182'; \n",
    "quit;\n",
    "```\n",
    "## Directing SAS to Use a Specified Index\n",
    "```SAS\n",
    "proc sql; \n",
    "    select *\n",
    "    from marchflights (idxname=daily)\n",
    "    where flightnumber='182'; \n",
    "quit;\n",
    "```\n",
    "## Dropping an Index\n",
    "```SAS\n",
    "proc sql;\n",
    "    drop index daily\n",
    "    from work.marchflights; \n",
    "quit;\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Chapter 7\n",
    "## Creating a PROC SQL View\n",
    "```SAS\n",
    "proc sql;\n",
    "    create view sasuser.raisev as\n",
    "    select empid, jobcode, salary format=dollar12.2, \n",
    "           salary/12 as MonthlySalary format=dollar12.\n",
    "    from payrollmaster\n",
    "    using libname airline 'c:\\data\\ia';\n",
    "quit;\n",
    "```\n",
    "## Displaying the Definition for a PROC SQL View\n",
    "```SAS\n",
    "proc sql;\n",
    "    describe view sasuser.raisev;\n",
    "quit;\n",
    "```\n",
    "## Using a PROC SQL View in a Query\n",
    "```SAS\n",
    "proc sql; \n",
    "    select *\n",
    "    from sasuser.raisev\n",
    "    where jobcode in ('PT2','PT3'); \n",
    "quit;\n",
    "```\n",
    "## Updating a PROC SQL View\n",
    "```SAS\n",
    "proc sql;\n",
    "    update sasuser.raisev\n",
    "    set salary=salary * 1.20\n",
    "    where jobcode='PT3'; \n",
    "quit;\n",
    "```\n",
    "## Dropping a PROC SQL View\n",
    "```SAS\n",
    "proc sql;\n",
    "    drop view sasuser.raisev;\n",
    "quit;\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Chapter 8\n",
    "## Querying a Table Using PROC SQL Options\n",
    "```SAS\n",
    "proc sql outobs=5;\n",
    "    select flightnumber, destination\n",
    "    from sasuser.internationalflights; \n",
    "reset number;\n",
    "    select flightnumber, destination \n",
    "    from sasuser.internationalflights \n",
    "    where boarded gt 200;\n",
    "quit;\n",
    "```\n",
    "## Describing and Querying a Dictionary Table\n",
    "```SAS\n",
    "proc sql;\n",
    "    describe table dictionary.columns; \n",
    "    select memname\n",
    "    from dictionary.columns \n",
    "    where libname='SASUSER' and name='EmpID';\n",
    "quit;\n",
    "```"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.6.5"
  },
  "latex_envs": {
   "LaTeX_envs_menu_present": true,
   "autoclose": false,
   "autocomplete": true,
   "bibliofile": "biblio.bib",
   "cite_by": "apalike",
   "current_citInitial": 1,
   "eqLabelWithNumbers": true,
   "eqNumInitial": 1,
   "hotkeys": {
    "equation": "Ctrl-E",
    "itemize": "Ctrl-I"
   },
   "labels_anchors": false,
   "latex_user_defs": false,
   "report_style_numbering": false,
   "user_envs_cfg": false
  },
  "toc": {
   "base_numbering": 1,
   "nav_menu": {},
   "number_sections": true,
   "sideBar": true,
   "skip_h1_title": false,
   "title_cell": "Table of Contents",
   "title_sidebar": "Contents",
   "toc_cell": false,
   "toc_position": {},
   "toc_section_display": true,
   "toc_window_display": false
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
